package Alliances

import Globals
import PlayerInfo

constant playerColorNames = [
    "red",
    "blue",
    "teal",
    "purple",
    "yellow",
    "orange",
    "green",
    "pink",
    "gray",
    "light blue",
    "dark green",
    "brown",
    "maroon",
    "navy",
    "turquoise",
    "violet",
    "wheat",
    "peach",
    "mint",
    "lavender",
    "coal",
    "snow",
    "emerald",
    "peanut"
]

// constant playercolors = [color(255,2,2), color(0,65,255), color(27,229,184), color(83,0,128), 
//     color(255,252,0), color(254,137,13), color(31,191,0), color(228,90,175), 
//     color(148,149,150), color(125,190,241),	color(15,97,69), color(77,41,3),
//     color(155,0,0), color(0, 0, 195), color(0, 234, 255), color(190, 0, 254), 
//     color(235, 205, 135), color(248, 164, 139), color(191, 255, 128), color(220, 185, 235), 
//     color(40, 40, 40), color(235, 240, 255), color(0, 120, 30), color(164, 11, 51)
//     ]

function getPlayerFromName(string name) returns player
    for i = 0 to bj_MAX_PLAYER_SLOTS
        if Player(i).getName() == name
            return Player(i)
    return null

function getPlayerFromColor(string color) returns player
    for i = 0 to bj_MAX_PLAYER_SLOTS
        if playerColorNames[i] == color.toLowerCase()
            return Player(i)
    return null

function getPlayerFromNumber(string num) returns player
    if num.toInt() < bj_MAX_PLAYERS
        return Player(num.toInt() - 1)
    return null

function getPlayerFromAnything(string str) returns player
    var p = getPlayerFromName(str)
    if p == null
        p = getPlayerFromColor(str)
    if p == null
        p = getPlayerFromNumber(str)
    return p


function handleChat()
    let msg = GetEventPlayerChatString()
    
    let p = GetTriggerPlayer()
    if msg.startsWith("-commands")
        printTimedToPlayer(kStrGold + "===== Commands =====", 20, p)
        printTimedToPlayer(kStrBlue + "-colors|r - display all player names, numbers, and color names.", 20, p)
        printTimedToPlayer(kStrBlue + "-players|r - display all player names, numbers, and color names.", 20, p)
        printTimedToPlayer(kStrBlue + "-ally <NAME/NUMBER/COLOR>|r - form an alliance with the given player using their name, number, or color.", 20, p)
        printTimedToPlayer(kStrBlue + "-truce <NAME/NUMBER/COLOR>|r - form a truce with the given player using their name, number, or color.", 20, p)
        printTimedToPlayer(kStrBlue + "-war <NAME/NUMBER/COLOR>|r - declare war on the given player using their name, number, or color.", 20, p)
    else if msg.startsWith("-colors") or msg.startsWith("-players") or msg.startsWith("-numbers")
        printTimedToPlayer(kStrGold + "===== Players in Game =====", 25, p)
        for i = 0 to bj_MAX_PLAYERS
            if players[i].isIngame()
                printTimedToPlayer(kStrBlue + "Player " +I2S(i+1) + ":|r " + players[i].getColor().toColor().toColorString() + Player(i).getName() + "|r is " + players[i].getColor().toColor().toColorString() + playerColorNames[i] + "|r.", 15, p)
    else if msg.startsWith("-ally")
        var name = msg.substring(6)
        let target = getPlayerFromAnything(name)
        
        if target != null
            if target != p
                if target.isActive()
                    SetPlayerAllianceStateBJ(p, target, bj_ALLIANCE_ALLIED_VISION)
                    // TODO make this a reciprical decision
                else
                    printTimedToPlayer("Player " + target.getColor().toColor().toColorString() + target.getName() + "|r is not a valid target for alliance.", 5, p)
            else
                printTimedToPlayer("You can't ally yourself!", 5, p)
        else 
            printTimedToPlayer("Could not find any player from \"" + name + "\".", 5, p)
    else if msg.startsWith("-war")
        var name = msg.substring(5)
        let target = getPlayerFromAnything(name)
        if target != null
            if target != p
                // TODO verify that the players are not in each other's bases/ heroes aren't near each other.
                SetPlayerAllianceStateBJ(p, target, bj_ALLIANCE_UNALLIED)
                SetPlayerAllianceStateBJ(target, p, bj_ALLIANCE_UNALLIED) // make sure that both are the status
            else
                printTimedToPlayer("You can't declare war on yourself!", 5, p)
        else 
            printTimedToPlayer("Could not find any player from \"" + name + "\".", 5, p)
    else if msg.startsWith("-truce")
        var name = msg.substring(7)
        let target = getPlayerFromAnything(name)

        if target != null
            if target != p
                if target.isActive()
                    SetPlayerAllianceStateBJ(p, target, bj_ALLIANCE_ALLIED)
                    // TODO make this a reciprical decision
                else
                    printTimedToPlayer("Player " + target.getColor().toColor().toColorString() + target.getName() + "|r is not a valid target for a truce.", 5, p)
            else
                printTimedToPlayer("You can't truce yourself!", 5, p)
        else 
            printTimedToPlayer("Could not find any player from \"" + name + "\".", 5, p)



init 
    var trg = CreateTrigger()
            ..addAction(function handleChat)
    for i = 0 to bj_MAX_PLAYERS
        trg.registerPlayerChatEvent(Player(i), "", false)
        Player(i).setName("[" + I2S(i+1) + "] " + Player(i).getName())